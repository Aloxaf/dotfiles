#compdef git-brws

_git-brws() {
  _arguments \
    {-r,--repo}'[Shorthand format or Git URL you want to see]' \
    {-b,--branch}'[Branch name to browse (default: current branch)]: :_git-brws_branch_names' \
    {-d,--dir}'[Directory path to the repository (default: current dir)]: :_files -/' \
    {-R,--remote}'[Remote name (default: the remote current branch tracking)]' \
    {-u,--url}'[Output URL to stdout instead of opening in browser]' \
    {-p,--pr}'[Open/Create pull request page instead of repository page]' \
    {-w,--website}'[Open website page instead of repository page]' \
    {-B,--blame}'[Open blame page instead of repository page]: :_files' \
    {-h,--help}'[Print this help]' \
    {-v,--version}'[Show version]' \
    '*:: :_files'
}

_git-brws_branch_names() {
  local -a branch_names
  branch_names=(${${(f)"$(_call_program remote-branch-refs git for-each-ref --format='"%(refname)"' refs/remotes 2>/dev/null)"}#refs/remotes/})
  _describe -t branch 'branch names' branch_names
}
